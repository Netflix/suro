ext.githubProjectName = rootProject.name

buildscript {
    repositories {
      mavenLocal()
      mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        //noinspection SpellCheckingInspection
        tasks.withType(Javadoc) {
            // disable the crazy super-strict doclint tool in Java 8
            //noinspection SpellCheckingInspection
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

allprojects {
    repositories { 
      mavenLocal()
      mavenCentral() // maven { url: 'http://jcenter.bintray.com' }
      maven{
        name 'github.release.repo'
        url 'https://raw.github.com/bulldog2011/bulldog-repo/master/repo/releases/'
      }
      maven {
        url "https://metamx.artifactoryonline.com/metamx/pub-libs-releases-local"
      }
      maven{
        name 'sonatype'
        url 'https://oss.sonatype.org/content/groups/public/'
      }
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

subprojects {
    apply plugin: 'idea'
    apply plugin: 'project-report'

    group = "com.netflix.${githubProjectName}"

    configurations {
      all*.exclude group: 'org.codehaus.jackson', module: 'jackson-mapper'
      all*.exclude group: 'org.codehaus.jackson', module: 'jackson-core'
    }

    dependencies {
      compile 'com.netflix.archaius:archaius-core:0.5.11'
      compile 'com.netflix.servo:servo-core:0.4.32'
      compile 'com.netflix.governator:governator:1.2.3'
      compile 'com.netflix.eureka:eureka-client:1.1.109'
      compile 'com.netflix.ribbon:ribbon-core:2.0-RC9'
      compile 'com.netflix.ribbon:ribbon-eureka:2.0-RC9'
      compile 'log4j:log4j:1.2.17'

      compile 'org.apache.thrift:libthrift:0.7.0'
      compile 'org.slf4j:slf4j-api:1.6.4'
      compile 'com.google.guava:guava:11.0.2'
      compile 'joda-time:joda-time:2.3'
      compile 'com.fasterxml.jackson.core:jackson-databind:2.1.4'
      compile 'com.fasterxml.jackson.core:jackson-annotations:2.1.4'
      compile 'com.fasterxml.jackson.core:jackson-core:2.1.4'
      compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.2.0'

      compile 'com.ning:compress-lzf:0.9.5'
      compile 'com.leansoft:bigqueue:0.7.0'
      compile 'commons-io:commons-io:2.4'

      runtime 'com.netflix.netflix-commons:netflix-commons-util:0.1.1'
      runtime 'org.slf4j:slf4j-log4j12:1.6.4'

      testCompile 'org.mockito:mockito-core:1.9.5'
      testCompile 'junit:junit:4.11'
      testCompile 'org.hamcrest:hamcrest-library:1.3'
    }

    test {
        forkEvery = 1
        maxParallelForks = 1
    }

    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
}

project(':suro-core') {
    dependencies {
        compile 'org.antlr:antlr-runtime:3.4'
        compile 'commons-jxpath:commons-jxpath:1.3'
    }
}

project(':suro-client') {
  dependencies {
    compile project(':suro-core')

    testCompile project(':suro-core').sourceSets.test.output
    
  }
}

project(':suro-kafka-producer') {
   test {
      maxParallelForks = 1
      forkEvery = 1
   }

  dependencies {
    compile project(':suro-core')

    compile 'org.apache.kafka:kafka-clients:0.8.2.0'

    testCompile('org.apache.kafka:kafka_2.9.2:0.8.2.0')
    testCompile 'org.apache.curator:curator-test:2.4.2'
    testCompile project(':suro-client')
  }
}
